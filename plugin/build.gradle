apply plugin: "java-gradle-plugin"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "maven-publish"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "com.trevjonez.adb-uninstall"

ext.artifactId = "plugin"

gradlePlugin {
    plugins {
        create("composer") {
            displayName = "com.trevjonez.composer"
            description = "Gradle task type and plugin for interacting with https://github.com/gojuno/composer"
            id = "com.trevjonez.composer"
            implementationClass = "com.trevjonez.composer.ComposerPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/trevjonez/composer-gradle-plugin"
    vcsUrl = "https://github.com/trevjonez/composer-gradle-plugin.git"
    tags = ["android", "composer", "test", "orchestrator", "report"]
}

dependencies {
    api("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    api("com.android.tools.build:gradle:$AGP_VERSION")
    api(gradleApi())

    testImplementation(gradleTestKit())
    testImplementation("junit:junit:4.12")
    testImplementation("org.assertj:assertj-core:3.5.2")
    testImplementation("commons-io:commons-io:2.5")
    testImplementation("org.jetbrains.kotlin:kotlin-reflect")
}

adbUninstall {
    packageNames.addAll(
            "com.trevjonez.andapp",
            "com.trevjonez.andapp.test",

            "com.trevjonez.anddyn",
            "com.trevjonez.anddyn.test",
            "com.trevjonez.atinstall.test",

            "com.trevjonez.andlib.test",

            "com.trevjonez.testapp",
            "com.trevjonez.testapp.test",
    )
}

tasks.named("test").configure {
    systemProperty("buildDir", buildDir.absolutePath)
    systemProperty("andApp", new File(rootProject.projectDir, "and-app").absolutePath)
    systemProperty("andLib", new File(rootProject.projectDir, "and-lib").absolutePath)
    systemProperty("andDyn", new File(rootProject.projectDir, "and-dyn").absolutePath)
    systemProperty("andTest", new File(rootProject.projectDir, "and-test").absolutePath)
    systemProperty("org.gradle.testkit.debug", false)

    outputs.dir("$buildDir/tests")
    outputs.dir(new File(rootProject.projectDir, "and-app/build"))
    outputs.dir(new File(rootProject.projectDir, "and-lib/build"))
    outputs.dir(new File(rootProject.projectDir, "and-dyn/build"))
    outputs.dir(new File(rootProject.projectDir, "and-dyn/base/build"))
    outputs.dir(new File(rootProject.projectDir, "and-dyn/atInstall/build"))

    inputs.files("gradle.properties")
    inputs.dir(new File(rootProject.projectDir, "and-app/src"))
    inputs.files(new File(rootProject.projectDir, "and-app/build.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-app/build-cascade-dsl.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-app/build-custom-task.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-app/settings.gradle"))

    inputs.dir(new File(rootProject.projectDir, "and-lib/src"))
    inputs.files(new File(rootProject.projectDir, "and-lib/build.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-lib/settings.gradle"))

    inputs.files(new File(rootProject.projectDir, "and-dyn/build.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-dyn/settings.gradle"))

    inputs.dir(new File(rootProject.projectDir, "and-dyn/base/src"))
    inputs.files(new File(rootProject.projectDir, "and-dyn/base/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "and-dyn/atInstall/src"))
    inputs.files(new File(rootProject.projectDir, "and-dyn/atInstall/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "and-test/app/src"))
    inputs.files(new File(rootProject.projectDir, "and-test/app/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "and-test/test/src"))
    inputs.files(new File(rootProject.projectDir, "and-test/test/build.gradle"))

    inputs.files(new File(rootProject.projectDir, "and-test/build.gradle"))
    inputs.files(new File(rootProject.projectDir, "and-test/settings.gradle"))

    inputs.dir(new File(rootProject.projectDir, "commander/os/src/main"))
    inputs.files(new File(rootProject.projectDir, "commander/os/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "commander/android/src/main"))
    inputs.files(new File(rootProject.projectDir, "commander/android/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "composer/composer/src/main"))
    inputs.files(new File(rootProject.projectDir, "composer/composer/build.gradle"))

    inputs.dir(new File(rootProject.projectDir, "composer/html-report"))

    dependsOn(":composer:publishToMavenLocal")
    dependsOn(":commander:os:publishToMavenLocal")
    dependsOn(":commander:android:publishToMavenLocal")
}

def sourcesJar = tasks.register("sourcesJar", Jar) {
    classifier = "sources"
    from(sourceSets["main"].allSource)
    dependsOn(sourceSets["main"].classesTaskName)
}

publishing {
    publications {
        register("plugin", MavenPublication) {
            from(components["java"])
            artifact(sourcesJar.get())

            pom {
                inceptionYear.set("2017")
                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        distribution.set("repo")
                    }
                }
            }
        }
    }
}